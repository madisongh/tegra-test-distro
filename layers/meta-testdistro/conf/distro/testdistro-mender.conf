include testdistro.conf

DISTRO = "testdistro-mender"
DISTRO_NAME = "Test Distro with Mender"
DISTRO_CODENAME .= "+mender"

# The distro version doesn't include DATE to avoid basehash changes in do_image_mender
DISTRO_VERSION = "${DISTRO_VERSION_BASE}+snapshot"

# For use in the /etc/os-release rewriter in
# mender-workarounds.bbclass
OS_RELEASE_VERSION ?= "${BUILDNAME} (${DISTRO_CODENAME})"

### Additions for mender ###
# distro_layer_buildinfo added to provide git hash in BUILDNAME
# (and thus MENDER_ARTIFACT_NAME)
INHERIT += "mender-full distro_layer_buildinfo"
INHERIT_append_tegra = " tegra-mender-setup mender-workarounds mendersign"
# DISTRO_LAYER defaults to "meta-${DISTRO}", which is not correct here.
DISTRO_LAYER = "meta-testdistro"

BUILDNAME ?= "${@d.getVar('DISTRO_VERSION').replace('+snapshot','')}${DISTRO_LAYER_BUILDTAG}"
BUILDNAME[vardepvalue] = "${DISTRO_VERSION}"

MENDER_ARTIFACT_NAME ?= "${IMAGE_BASENAME}_${BUILDNAME}"

TEGRA_IMAGE_FSTYPES = "tegraflash mender dataimg"
TEGRA_IMAGE_FSTYPES_secureboot = "tar.gz mender dataimg"
IMAGE_FSTYPES_tegra = "${TEGRA_IMAGE_FSTYPES}"
IMAGE_FSTYPES_pn-tegra-minimal-initramfs_tegra = "${INITRAMFS_FSTYPES}"

MENDER_STORAGE_DEVICE_BASE ?= "/dev/mmcblk0p"

IMAGE_INSTALL_append_tegra = " tegra-eeprom-tool i2c-tools tegra-bup-payload"

MENDER_FEATURES_ENABLE_remove_secureboot = "mender-growfs-data"

KERNEL_ARGS_append = " loglevel=3"
KERNEL_ARGS_remove = "console=tty0"

# Machine-specific overrides for "semi-stateless" setup:
#   - enable systemd volatile root in 'overlay' mode,
#     with systemd in the initramfs (via td-initramfs)
#   - expect UDA (/data) partition to be 16MB
#   - additional partitions:
#       - LOGS (for /var/log), fixed size
#       - EXTRA (for /var/extra), last partition in eMMC
#   - overlay mounts for /var/lib and /home are placed on
#     /var/extra, as is the Docker storage area.
MACHINEOVERRIDES_prepend_tegra = "semi-stateless:"
KERNEL_ARGS_append_tegra_semi-stateless = " systemd.volatile=overlay"
INITRAMFS_IMAGE_semi-stateless = "td-initramfs"
# Default to 4GiB rootfs part size for 16GiB units
ROOTFSPART_SIZE_tegra_semi-stateless = "4294967296"
# Use 8GiB for 32GiB units
ROOTFSPART_SIZE_jetson-xavier_semi-stateless = "8589934592"
ROOTFSPART_SIZE_jetson-tx2_semi-stateless = "8589934592"

MENDER_FEATURES_ENABLE_remove_semi-stateless = "mender-growfs-data"
MENDER_DATA_PART_SIZE_MB_DEFAULT_semi-stateless = "16"

# The following is added by mender-setup-systemd.inc, inappropriately
DISTRO_FEATURES_BACKFILL_remove = "systemd"
# Don't let Mender modify e2fsprogs or systemd
BBMASK += "/meta-mender-core/recipes-devtools/e2fsprogs/"
BBMASK += "/meta-mender-core/recipes-core/systemd/"

# Disable run-postinsts
ROOTFS_BOOTSTRAP_INSTALL_semi-stateless = ""
