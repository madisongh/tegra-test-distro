DISTRO = "testdistro"
DISTRO_NAME = "Test Distro"
DISTRO_VERSION_BASE = "3.1.3"
DISTRO_VERSION = "${DISTRO_VERSION_BASE}+snapshot-${DATE}"
DISTRO_CODENAME = "dunfell-l4t-r32.5.0"
SDK_VENDOR = "-tdsdk"
SDK_VERSION := "${@'${DISTRO_VERSION}'.replace('snapshot-${DATE}','snapshot')}"

MAINTAINER = "OE4Tegra team <oe4tegra@madison.systems>"

TARGET_VENDOR = "-oe4t"

# New ${DISTRO}-<version> setting for sanity checks.
# Increment version number (and the corresponding
# setting int the template bblayers.conf.sample file)
# each time the layer settings are changed.
REQUIRED_TD_BBLAYERS_CONF_VERSION = "${DISTRO}-3"

LOCALCONF_VERSION = "1"

DISTRO_VERSION[vardepsexclude] = "DATE"
SDK_VERSION[vardepsexclude] = "DATE"

TD_DEFAULT_DISTRO_FEATURES = "largefile opengl ptest multiarch wayland vulkan systemd pam virtualization"

DISTRO_FEATURES ?= "${DISTRO_FEATURES_DEFAULT} ${TD_DEFAULT_DISTRO_FEATURES}"

# Jetson platforms do not use linux-yocto, but for QEMU testing
# align with the poky distro.
PREFERRED_VERSION_linux-yocto ?= "5.4%"

# Use keystore TA
PREFERRED_PROVIDER_virtual/secure-os_tegra186 = "tos-keystore"
PREFERRED_PROVIDER_virtual/secure-os_tegra194 = "tos-keystore"

# Gstreamer libraries are passed through to containers when using
# nvidia-docker, so our version of Gstreamer must match the one in
# the stock L4T/JetPack release.
require conf/include/gstreamer-1.14.conf

SDK_NAME = "${DISTRO}-${TCLIBC}-${SDKMACHINE}-${IMAGE_BASENAME}-${TUNE_PKGARCH}-${MACHINE}"
SDKPATH = "/opt/${DISTRO}/${SDK_VERSION}"

TCLIBCAPPEND = ""

PREMIRRORS ??= "\
bzr://.*/.*   http://downloads.yoctoproject.org/mirror/sources/ \n \
cvs://.*/.*   http://downloads.yoctoproject.org/mirror/sources/ \n \
git://.*/.*   http://downloads.yoctoproject.org/mirror/sources/ \n \
gitsm://.*/.* http://downloads.yoctoproject.org/mirror/sources/ \n \
hg://.*/.*    http://downloads.yoctoproject.org/mirror/sources/ \n \
osc://.*/.*   http://downloads.yoctoproject.org/mirror/sources/ \n \
p4://.*/.*    http://downloads.yoctoproject.org/mirror/sources/ \n \
svn://.*/.*   http://downloads.yoctoproject.org/mirror/sources/ \n"

SANITY_TESTED_DISTROS ?= " \
            ubuntu-18.04 \n \
            ubuntu-20.04 \n \
            ubuntu-20.10 \n \
            "

# CUDA 10.2 requires gcc 7 or 8
GCCVERSION_aarch64 = "8.%"
SDKGCCVERSION = "9.%"

# Most NVIDIA-supplied services expect systemd
INIT_MANAGER = "systemd"

INHERIT += "tegra-support-sanity"

require conf/distro/include/no-static-libs.inc
require conf/distro/include/yocto-uninative.inc
require conf/distro/include/security_flags.inc
INHERIT += "uninative"

INHERIT += "reproducible_build"

LICENSE_FLAGS_WHITELIST += "commercial_faad2"

IMAGE_CLASSES_append_tegra = " tegrasign image_extras"

# No syslog in these builds, using systemd-journald directly.
# On platforms with a /var/log partition, logs will persist
VOLATILE_LOG_DIR = "no"
VIRTUAL-RUNTIME_base-utils-syslog = ""
# No /var/volatile hackery in these builds - not needed
FILESYSTEM_PERMS_TABLES = "files/fs-perms-no-volatile.txt"
# Ensure that the initramfs and main rootfs use the same UIDs/GIDs.
# This is important if using systemd in the initramfs.
USERADDEXTENSION = "useradd-staticids"
USERADD_ERROR_DYNAMIC = "error"
IMGCLASSES_append = " rootfs-postcommands-overrides"
