DISTRO = "testdistro"
DISTRO_NAME = "Test Distro"
DISTRO_VERSION_BASE = "4.0"
DISTRO_VERSION = "${DISTRO_VERSION_BASE}+snapshot-${DATE}"
DISTRO_CODENAME = "master"
SDK_VENDOR = "-tdsdk"
SDK_VERSION := "${@'${DISTRO_VERSION}'.replace('snapshot-${DATE}','snapshot')}"

MAINTAINER = "OE4Tegra team <oe4tegra@madison.systems>"

TARGET_VENDOR = "-oe4t"

# New ${DISTRO}-<version> setting for sanity checks.
# Increment version number (and the corresponding
# setting int the template bblayers.conf.sample file)
# each time the layer settings are changed.
REQUIRED_TD_BBLAYERS_CONF_VERSION = "${DISTRO}-5"

LOCALCONF_VERSION = "2"

DISTRO_VERSION[vardepsexclude] = "DATE"
SDK_VERSION[vardepsexclude] = "DATE"

TD_DEFAULT_DISTRO_FEATURES = "largefile opengl ptest multiarch wayland vulkan systemd pam virtualization"
TD_EXTRA_DISTRO_FEATURES ?= "rauc"

DISTRO_FEATURES ?= "${DISTRO_FEATURES_DEFAULT} ${TD_DEFAULT_DISTRO_FEATURES} ${TD_EXTRA_DISTRO_FEATURES}"

# Jetson platforms do not use linux-yocto, but for QEMU testing
# align with the poky distro.
PREFERRED_VERSION_linux-yocto ?= "5.14%"
PREFERRED_VERSION_linux-yocto-rt ?= "5.14%"

# Use keystore TA
PREFERRED_PROVIDER_virtual/secure-os:tegra186 = "tos-keystore"
PREFERRED_PROVIDER_virtual/secure-os:tegra194 = "tos-keystore"

# Gstreamer libraries are passed through to containers when using
# nvidia-docker, so our version of Gstreamer must match the one in
# the stock L4T/JetPack release.
require conf/include/gstreamer-1.14.conf

SDK_NAME = "${DISTRO}-${TCLIBC}-${SDKMACHINE}-${IMAGE_BASENAME}-${TUNE_PKGARCH}-${MACHINE}"
SDKPATH = "/opt/${DISTRO}/${SDK_VERSION}"

TCLIBCAPPEND = ""

PREMIRRORS ??= "\
bzr://.*/.*   http://downloads.yoctoproject.org/mirror/sources/ \n \
cvs://.*/.*   http://downloads.yoctoproject.org/mirror/sources/ \n \
git://.*/.*   http://downloads.yoctoproject.org/mirror/sources/ \n \
gitsm://.*/.* http://downloads.yoctoproject.org/mirror/sources/ \n \
hg://.*/.*    http://downloads.yoctoproject.org/mirror/sources/ \n \
osc://.*/.*   http://downloads.yoctoproject.org/mirror/sources/ \n \
p4://.*/.*    http://downloads.yoctoproject.org/mirror/sources/ \n \
svn://.*/.*   http://downloads.yoctoproject.org/mirror/sources/ \n"

SANITY_TESTED_DISTROS ?= " \
            ubuntu-18.04 \n \
            ubuntu-20.04 \n \
            ubuntu-21.04 \n \
            ubuntu-21.10 \n \
            ubuntu-22.04 \n \
            "

# CUDA 10.2 requires gcc 8
CUDA_GCCVERSION = "8.%"

# Most NVIDIA-supplied services expect systemd
INIT_MANAGER = "systemd"

INHERIT += "tegra-support-sanity distro_layer_buildinfo"
SDK_INHERT_BLACKLIST:append = " tegra-support-sanity"

require conf/distro/include/no-static-libs.inc
require conf/distro/include/yocto-uninative.inc
require conf/distro/include/security_flags.inc
INHERIT += "uninative"

LICENSE_FLAGS_ACCEPTED += "commercial_faad2"

PREFERRED_PROVIDER_virtual/bootlogo:tegra = "bootlogo-custom"
IMAGE_CLASSES:append:tegra = " tegrasign image_extras"

# No syslog in these builds, using systemd-journald directly.
# On platforms with a /var/log partition, logs will persist
VOLATILE_LOG_DIR = "no"
VIRTUAL-RUNTIME_base-utils-syslog = ""
# No /var/volatile hackery in these builds - not needed
FILESYSTEM_PERMS_TABLES = "files/fs-perms-no-volatile.txt"
# Ensure that the initramfs and main rootfs use the same UIDs/GIDs.
# This is important if using systemd in the initramfs.
USERADDEXTENSION = "useradd-staticids"
USERADD_ERROR_DYNAMIC = "error"
IMGCLASSES:append = " rootfs-postcommands-overrides"

KERNEL_ARGS:append = " loglevel=3"
KERNEL_ARGS:remove = "console=tty0"

# Machine-specific overrides for "semi-stateless" setup:
#   - enable systemd volatile root in 'overlay' mode,
#     with systemd in the initramfs (via td-initramfs)
#   - additional partitions:
#       - LOGS (for /var/log), fixed size
#       - EXTRA (for /var/extra), last partition in eMMC
#   - overlay mounts for /var/lib and /home are placed on
#     /var/extra, as is the Docker storage area.
MACHINEOVERRIDES:prepend:tegra = "semi-stateless:"
KERNEL_ARGS:append:tegra:semi-stateless = " systemd.volatile=overlay"
INITRAMFS_IMAGE:pn-linux-tegra:semi-stateless = "td-initramfs"
# Default to 4GiB rootfs part size for 16GiB units
ROOTFSPART_SIZE:tegra:semi-stateless = "4294967296"
# Use 8GiB for 32GiB units
ROOTFSPART_SIZE:jetson-xavier:semi-stateless = "8589934592"
ROOTFSPART_SIZE:jetson-tx2-devkit:semi-stateless = "8589934592"
# Disable run-postinsts
ROOTFS_BOOTSTRAP_INSTALL:semi-stateless = ""

RAUC_KEY_FILE ?= "${COREBASE}/meta-testdistro/files/rauc/development-1.key.pem"
RAUC_CERT_FILE ?= "${COREBASE}/meta-testdistro/files/rauc/development-1.cert.pem"
SKIP_META_RAUC_FEATURE_CHECK = "1"
