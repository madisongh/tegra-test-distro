include testdistro.conf

DISTRO = "testdistro-mender"
DISTRO_NAME = "Test Distro with Mender"
DISTRO_CODENAME .= "+mender"

# The distro version doesn't include DATE to avoid basehash changes in do_image_mender
DISTRO_VERSION = "${DISTRO_VERSION_BASE}+snapshot"

# For use in the /etc/os-release rewriter in
# mender-workarounds.bbclass
OS_RELEASE_VERSION ?= "${BUILDNAME} (${DISTRO_CODENAME})"

### Additions for mender ###
# distro_layer_buildinfo added to provide git hash in BUILDNAME
# (and thus MENDER_ARTIFACT_NAME)
INHERIT += "mender-full distro_layer_buildinfo"
INHERIT_append_tegra = " tegra-mender-setup mender-workarounds mendersign"
# DISTRO_LAYER defaults to "meta-${DISTRO}", which is not correct here.
DISTRO_LAYER = "meta-testdistro"

BUILDNAME ?= "${@d.getVar('DISTRO_VERSION').replace('+snapshot','')}${DISTRO_LAYER_BUILDTAG}"
BUILDNAME[vardepvalue] = "${DISTRO_VERSION}"

MENDER_ARTIFACT_NAME ?= "${IMAGE_BASENAME}_${BUILDNAME}"

IMAGE_FSTYPES_tegra = "tegraflash mender dataimg"
IMAGE_FSTYPES_secureboot = "tar.gz mender dataimg"
IMAGE_FSTYPES_pn-tegra-minimal-initramfs_tegra = "${INITRAMFS_FSTYPES}"

# Reserved space for tegraflash partitions, not including APP and DATA
TEGRA_FLASH_PARTITIONS_MB ?= "950"

MENDER_STORAGE_DEVICE_BASE ?= "/dev/mmcblk0p"
MENDER_STORAGE_TOTAL_SIZE_MB_DEFAULT_tegra = "${@tegra_mender_calc_total_size(d) - int(d.getVar('TEGRA_FLASH_PARTITIONS_MB'))}"

# Mender machines need to run checks after an update
SYSTEMD_DEFAULT_TARGET = "finished-booting.target"

IMAGE_INSTALL_append_tegra = " tegra-eeprom-tool i2c-tools tegra-bup-payload"

MENDER_FEATURES_ENABLE_remove_secureboot = "mender-growfs-data"
LIBUBOOTENVBIN_PROVIDER = "libubootenv-bin"
LIBUBOOTENVBIN_PROVIDER_tegra = "${@'libubootenv-fake' if d.getVar('PREFERRED_PROVIDER_virtual/bootloader').startswith('cboot') else 'libubootenv-bin'}"
PREFERRED_RPROVIDER_libubootenv-bin = "${LIBUBOOTENVBIN_PROVIDER}"
PREFERRED_PROVIDER_libubootenv_tegra = "${@'libubootenv-fake' if d.getVar('PREFERRED_PROVIDER_virtual/bootloader').startswith('cboot') else 'libubootenv'}"
